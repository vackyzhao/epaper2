#include <epd2in9_V2.h>
#include <epdpaint.h>
#include <imagedata.h>

#include <Wire.h>
#include <EEPROM.h>
#include <limits.h>

#include <avr/sleep.h>
#include <avr/power.h>
#include <avr/interrupt.h>
#include <battery_monitor.h>

#include <EEPROM.h>
#include <RTClib.h>




#define PMOS_CTRL_PIN 5
#define SERIAL_BUFFER_SIZE 128
#define COLORED 0
#define UNCOLORED 1

char serialBuffer[SERIAL_BUFFER_SIZE];
unsigned char image[512];
Paint paint(image, 0, 0);  // width should be the multiple of 8

RTC_DS3231 rtc;
Epd epd;
bool firstFlag = true;  // ç”¨äºç¬¬ä¸€æ¬¡æ˜¾ç¤ºæ—¶é—´æ—¶çš„ç‰¹æ®Šå¤„ç†
volatile bool wakeUp = false;
volatile bool alarmTriggered = false;
volatile bool button1Pressed = false;  // D2
volatile bool button2Pressed = false;  // D3
volatile bool button3Pressed = false;  // A7

DateTime lastDisplayTime;  // å…¨å±€å˜é‡ï¼Œè®°å½•ä¸Šä¸€æ¬¡æ˜¾ç¤ºçš„æ—¶é—´ï¼ˆå»ºè®®åªæ¯”è¾ƒå¹´æœˆæ—¥ï¼‰
uint8_t lastDay = 255;
uint16_t todayMin = 0;
uint32_t totalMin = 0;
 char timeBuf_old[6];

// â€”â€”â€” ä»»åŠ¡è°ƒåº¦å‚æ•° â€”â€”â€”
const uint8_t TASK_BASE_H = 2;    // èµ·ç‚¹å°æ—¶ 2 (= 02:05)
const uint8_t TASK_INTERVAL = 6;  // æ¯ 6 å°æ—¶
const uint8_t TASK_MINUTE = 5;    // å›ºå®šåœ¨ xx:05

// ä¸Šä¸€æ¬¡çœŸæ­£æ‰§è¡Œä»»åŠ¡çš„â€œæ—¥ + æ—¶â€
static uint8_t lastTaskDay = 0xFF;
static int8_t lastTaskHour = -1;

void displayTime(DateTime now) ;

enum SystemState {
  STATE_EXAM_COUNTDOWN,
  STATE_MEET_COUNTDOWN,
  STATE_FOCUS_PAUSED,
  STATE_FOCUS_RUNNING,
  STATE_MQTT_MESSAGE,
  STATE_LOW_BATTERY
};
enum EventType {
  EVENT_BUTTON1,
  EVENT_BUTTON2,
  EVENT_BUTTON3
};

SystemState currentState = STATE_EXAM_COUNTDOWN;
SystemState lastState = STATE_EXAM_COUNTDOWN;


void drawicon(int status);
void drawFocus(int status);
void drawLowBatteryUI();
void setupNextAlarm();



void checkMessages(void) {
  uint16_t voltage = readBatteryVoltage_mv(5);
  digitalWrite(PMOS_CTRL_PIN, LOW);  // æ‰“å¼€ç”µæº
  delay(5000);
  Serial.println("AT+CEREG?"); 
  Serial.flush();
  delay(1000);
  Serial.println("AT+CIPGSMLOC=1,1");
  Serial.flush();
  delay(1000);

  Serial.println("AT+MCONFIG=\"wjy_air780e\",\"wjy\",\"1234asdf\",1,1,\"wjy_air780e/status\",\"offline\"");
  Serial.flush();
  delay(1000);

  Serial.println("AT+MIPSTART=\"cow.milkcat.cc\",1883");
  Serial.flush();
  delay(1000);


  Serial.println("AT+MCONNECT=0,60");
  Serial.flush();
  delay(1000);

  
  Serial.println("AT+MDISCONNECT");
  Serial.flush();
  delay(1000);


  Serial.println("AT+MIPCLOSE");
  Serial.flush();
  delay(1000);

  delay(3000);
  digitalWrite(PMOS_CTRL_PIN, HIGH);  // æ‰“å¼€ç”µæº
}


void saveTotalMin(uint32_t totalMin) {
  const uint16_t addr_magic = 0x30;
  const uint8_t magic_val = 0xA6;
  const uint16_t addr_data = 0x31;

  EEPROM.update(addr_magic, magic_val);

  for (uint8_t i = 0; i < 4; ++i) {
    EEPROM.update(addr_data + i, (totalMin >> (8 * i)) & 0xFF);
  }
  //debugSerial.print("save: ");
  //debugSerial.println(totalMin);
}
bool loadTotalMin(uint32_t &totalMin) {
  const uint16_t addr_magic = 0x30;
  const uint8_t magic_val = 0xA6;
  const uint16_t addr_data = 0x31;

  if (EEPROM.read(addr_magic) != magic_val) {
    totalMin = 0;
    return false;
  }

  totalMin = 0;
  for (uint8_t i = 0; i < 4; ++i) {
    totalMin |= ((uint32_t)EEPROM.read(addr_data + i)) << (8 * i);
  }
  //debugSerial.print("load: ");
  //debugSerial.println(totalMin);
  return true;
}



// è¯»å–ï¼šä»å›ºå®šåœ°å€å– MAGIC/YY/MM/DDï¼ŒæˆåŠŸåˆ™å†™å…¥ outï¼ˆyear = 2000 + YYï¼‰
bool eepromLoadTarget(DateTime &out) {
  const uint16_t addr = 0x0020;    // å­˜æ”¾åœ°å€ï¼ˆå¯æ”¹ï¼‰
  const uint8_t magicWant = 0xA5;  // MAGICï¼ˆå¯æ”¹ï¼‰
  const uint16_t baseYear = 2000;

  if (addr + 3 >= EEPROM.length())
    return false;  // è¶Šç•Œä¿æŠ¤

  if (EEPROM.read(addr + 0) != magicWant)
    return false;
  const uint8_t yy = EEPROM.read(addr + 1);
  const uint8_t mm = EEPROM.read(addr + 2);
  const uint8_t dd = EEPROM.read(addr + 3);

  if (mm < 1 || mm > 12 || dd < 1 || dd > 31)
    return false;

  out = DateTime((uint16_t)(baseYear + yy), mm, dd);
  return true;
}

// å†™å…¥ï¼šæŠŠ DateTime å‹ç¼©æˆ YY/MM/DD + MAGIC å­˜åˆ°å›ºå®šåœ°å€
void eepromSaveTarget(const DateTime &dt) {
  const uint16_t addr = 0x0020;    // å­˜æ”¾åœ°å€ï¼ˆå¯æ”¹ï¼‰
  const uint8_t magicWant = 0xA5;  // MAGICï¼ˆå¯æ”¹ï¼‰
  const uint16_t baseYear = 2000;

  if (addr + 3 >= EEPROM.length())
    return;  // è¶Šç•Œä¿æŠ¤

  uint16_t y = dt.year();
  uint8_t m = dt.month();
  uint8_t d = dt.day();

  // åˆæ³•æ€§ä¸å¤¹å–
  if (y < baseYear)
    y = baseYear;
  uint8_t yy = (uint8_t)((y - baseYear) > 255 ? 255 : (y - baseYear));
  if (m < 1 || m > 12)
    m = 1;
  if (d < 1 || d > 31)
    d = 1;

  EEPROM.update(addr + 0, magicWant);
  EEPROM.update(addr + 1, yy);
  EEPROM.update(addr + 2, m);
  EEPROM.update(addr + 3, d);
}

void switchState(EventType event) {
  // çŠ¶æ€è¿ç§»é€»è¾‘ï¼šè¾“å…¥äº‹ä»¶ + å½“å‰çŠ¶æ€ => ä¸‹ä¸€ä¸ªçŠ¶æ€
  lastState = currentState;
  switch (currentState) {
    case STATE_EXAM_COUNTDOWN:
      if (event == EVENT_BUTTON1)
        currentState = STATE_MEET_COUNTDOWN;
      else if (event == EVENT_BUTTON2)
        currentState = STATE_FOCUS_RUNNING;
      else if (event == EVENT_BUTTON3)
        currentState = STATE_MQTT_MESSAGE;
      break;

    case STATE_MEET_COUNTDOWN:
      if (event == EVENT_BUTTON1)
        currentState = STATE_EXAM_COUNTDOWN;
      else if (event == EVENT_BUTTON2)
        currentState = STATE_FOCUS_RUNNING;
      else if (event == EVENT_BUTTON3)
        currentState = STATE_MQTT_MESSAGE;
      break;

    case STATE_FOCUS_RUNNING:
      saveTotalMin(totalMin);
      if (event == EVENT_BUTTON1)
        currentState = STATE_EXAM_COUNTDOWN;
      else if (event == EVENT_BUTTON2)
        currentState = STATE_FOCUS_PAUSED;
      else if (event == EVENT_BUTTON3)
        currentState = STATE_MQTT_MESSAGE;
      break;

    case STATE_FOCUS_PAUSED:
      saveTotalMin(totalMin);
      if (event == EVENT_BUTTON1)
        currentState = STATE_EXAM_COUNTDOWN;
      else if (event == EVENT_BUTTON2)
        currentState = STATE_FOCUS_RUNNING;
      else if (event == EVENT_BUTTON3)
        currentState = STATE_MQTT_MESSAGE;
      break;

    case STATE_MQTT_MESSAGE:
      if (event == EVENT_BUTTON1)
        currentState = STATE_EXAM_COUNTDOWN;
      else if (event == EVENT_BUTTON2)
        currentState = STATE_FOCUS_RUNNING;
      else if (event == EVENT_BUTTON3)
        currentState = STATE_MQTT_MESSAGE;
      break;

    case STATE_LOW_BATTERY:
      // ä¸å…è®¸é€€å‡ºä½ç”µçŠ¶æ€ï¼Œç›´åˆ°é‡å¯æˆ–å”¤é†’
      return;
  }
  //debugSerial.println(currentState);
  // ç•Œé¢æ›´æ–°ï¼ˆé›†ä¸­ç»Ÿä¸€ï¼‰
  switch (currentState) {
    case STATE_EXAM_COUNTDOWN:
      drawicon(0);
      epd.DisplayFrame(); 
      displayTime(rtc.now());
      break;
    case STATE_MEET_COUNTDOWN:
      drawicon(1);
      epd.DisplayFrame(); 
      displayTime(rtc.now());
      break;
    case STATE_FOCUS_PAUSED:
      saveTotalMin(totalMin);
      drawFocus(0);
      break;
    case STATE_FOCUS_RUNNING:
      loadTotalMin(totalMin);
      drawFocus(1);
      break;
    case STATE_MQTT_MESSAGE:
      checkMessages();
      break;
    case STATE_LOW_BATTERY:
      drawLowBatteryUI();
      break;
  }
}

ISR(INT0_vect) {
  button1Pressed = true;
}
ISR(INT1_vect) {
  button2Pressed = true;
}
// D8-D13 å¯¹åº” PCINT0 å‘é‡ï¼ˆPORTBï¼‰
ISR(PCINT0_vect) {
  if (!(PINB & (1 << PINB5))) {
    // D13 ä¸ºä½ç”µå¹³ï¼ˆå¯èƒ½æ˜¯ RTC è§¦å‘çš„ä¸­æ–­ï¼‰
    alarmTriggered = true;
  } else {
    // D13 ä¸ºé«˜ç”µå¹³
  }
}
ISR(PCINT1_vect) {
  if (!(PINC & (1 << PINC1))) {
    // A1 ä¸ºä½ç”µå¹³ï¼ˆæ¯”å¦‚æŒ‰é’®æŒ‰ä¸‹ï¼‰
    button3Pressed = true;
  } else {
    // A1 ä¸ºé«˜ç”µå¹³
  }
}

void displayFocus(DateTime now) {
  epd.Init();

  char timeBuf[6];  // "HH:MM"
  snprintf(timeBuf, sizeof(timeBuf), "%02d:%02d", now.hour(), now.minute());

  // è®¾ç½®ç”»å¸ƒæ–¹å‘å’Œå¤§å°ï¼ˆé€‚åˆç«–ç›´æ–‡å­—ï¼‰
  paint.SetWidth(14);          // å­—ä½“å®½åº¦ä¸º 32px
  paint.SetHeight(148);        // é«˜åº¦ä¸º 96pxï¼Œå®¹çº³æ•´ä¸ªæ–‡å­—
  paint.SetRotate(ROTATE_90);  // é¡ºæ—¶é’ˆæ—‹è½¬ 90Â°ï¼Œç«–æ’

  // ç¬¬ä¸€æ­¥ï¼šæ¸…ç©ºæ—§å†…å®¹
  epd.SetPartialRefresh();
  paint.Clear(UNCOLORED);
  epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 110, 180, paint.GetWidth(), paint.GetHeight());
  epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 50, 180, paint.GetWidth(), paint.GetHeight());
  epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 10, 180, paint.GetWidth(), paint.GetHeight());

  paint.SetWidth(32);          // å­—ä½“å®½åº¦ä¸º 32px
  paint.SetHeight(32);         // é«˜åº¦ä¸º 96pxï¼Œå®¹çº³æ•´ä¸ªæ–‡å­—
  paint.SetRotate(ROTATE_90);  // é¡ºæ—¶é’ˆæ—‹è½¬ 90Â°ï¼Œç«–æ’

  if (currentState == STATE_FOCUS_RUNNING) {
    if (lastState != STATE_FOCUS_RUNNING) {
      paint.Clear(UNCOLORED);
      epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 30, 10, paint.GetWidth(), paint.GetHeight());
    }
  } else if (currentState == STATE_FOCUS_PAUSED) {
    if (lastState != STATE_FOCUS_PAUSED) {
      paint.Clear(UNCOLORED);
      epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 30, 10, paint.GetWidth(), paint.GetHeight());
    }
  }

  epd.DisplayFrame_Partial();
  delay(100);  // å»¶è¿Ÿç”¨äºé˜²æ­¢æ®‹å½±å’Œåˆ·æ–°å¹²æ‰°

  char todayStr[16];  // e.g., "1h 23m"
  char totalStr[16];  // e.g., "54h 01m"

  paint.SetWidth(32);          // å­—ä½“å®½åº¦ä¸º 32px
  paint.SetHeight(32);         // é«˜åº¦ä¸º 96pxï¼Œå®¹çº³æ•´ä¸ªæ–‡å­—
  paint.SetRotate(ROTATE_90);  // é¡ºæ—¶é’ˆæ—‹è½¬ 90Â°ï¼Œç«–æ’

  if (currentState == STATE_FOCUS_RUNNING) {
    todayMin += 1;  // æ¯åˆ†é’Ÿå¢åŠ 1
    totalMin += 1;

    paint.Clear(UNCOLORED);
    epd.SetPartialRefresh();
    paint.DrawStringAt(0, 0, " ", &Font00, COLORED);
    epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 30, 10, paint.GetWidth(), paint.GetHeight());

  } else if (currentState == STATE_FOCUS_PAUSED) {
    paint.Clear(UNCOLORED);
    epd.SetPartialRefresh();
    paint.DrawFilledRectangle(6, 4, 12, 28, COLORED);   // å·¦æ¡
    paint.DrawFilledRectangle(20, 4, 26, 28, COLORED);  // å³æ¡
    epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 30, 10, paint.GetWidth(), paint.GetHeight());
    // æš‚åœçŠ¶æ€ä¸å¢åŠ è®¡æ—¶
  }


  // ç¬¬äºŒæ­¥ï¼šç»˜åˆ¶æ–°æ—¶é—´
  // è®¾ç½®ç”»å¸ƒæ–¹å‘å’Œå¤§å°ï¼ˆé€‚åˆç«–ç›´æ–‡å­—ï¼‰
  paint.SetWidth(14);          // å­—ä½“å®½åº¦ä¸º 32px
  paint.SetHeight(148);        // é«˜åº¦ä¸º 96pxï¼Œå®¹çº³æ•´ä¸ªæ–‡å­—
  paint.SetRotate(ROTATE_90);  // é¡ºæ—¶é’ˆæ—‹è½¬ 90Â°ï¼Œç«–æ’
  paint.Clear(UNCOLORED);
  paint.DrawStringAt(0, 0, timeBuf, &Font20, COLORED);
  epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 110, 180, paint.GetWidth(), paint.GetHeight());
  paint.Clear(UNCOLORED);
  snprintf(todayStr, sizeof(todayStr), "%uh %02um", todayMin / 60, todayMin % 60);
  paint.DrawStringAt(0, 0, todayStr, &Font20, COLORED);
  epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 50, 160, paint.GetWidth(), paint.GetHeight());
  paint.Clear(UNCOLORED);
  snprintf(totalStr, sizeof(totalStr), "%luh %02lum", totalMin / 60, totalMin % 60);
  paint.DrawStringAt(0, 0, totalStr, &Font20, COLORED);
  epd.SetFrameMemory_Partial_NoRefresh(paint.GetImage(), 10, 160, paint.GetWidth(), paint.GetHeight());
  paint.Clear(UNCOLORED);
  epd.DisplayFrame_Partial();
  delay(100);
  epd.Sleep();
  delay(100);
}

void displayTime(DateTime now) {
  epd.Init();
  char timeBuf[6];  // "HH:MM"
  snprintf(timeBuf, sizeof(timeBuf), "%02d:%02d", now.hour(), now.minute());

  // è®¾ç½®ç”»å¸ƒæ–¹å‘å’Œå¤§å°ï¼ˆé€‚åˆç«–ç›´æ–‡å­—ï¼‰
  paint.SetWidth(32);          // å­—ä½“å®½åº¦ä¸º 32px
  paint.SetHeight(96);         // é«˜åº¦ä¸º 96pxï¼Œå®¹çº³æ•´ä¸ªæ–‡å­—
  paint.SetRotate(ROTATE_90);  // é¡ºæ—¶é’ˆæ—‹è½¬ 90Â°ï¼Œç«–æ’

  if(firstFlag==true) {
    firstFlag=false;
  } else {
    paint.Clear(UNCOLORED);
    paint.DrawStringAt(0, 4, timeBuf_old, &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 64, 168, paint.GetWidth(), paint.GetHeight());//è®¾ç½®ä¸Šä¸€æ¬¡çš„base
  }


  // ç¬¬ä¸€æ­¥ï¼šæ¸…ç©ºæ—§å†…å®¹
  paint.Clear(UNCOLORED);
  epd.SetFrameMemory_Partial(paint.GetImage(), 64, 168, paint.GetWidth(), paint.GetHeight());
  epd.DisplayFrame_Partial();
  delay(1000);  // å»¶è¿Ÿç”¨äºé˜²æ­¢æ®‹å½±å’Œåˆ·æ–°å¹²æ‰°


  // ç¬¬äºŒæ­¥ï¼šç»˜åˆ¶æ–°æ—¶é—´
  paint.Clear(UNCOLORED);
  paint.DrawStringAt(0, 4, timeBuf, &Font20, COLORED);
  epd.SetFrameMemory_Partial(paint.GetImage(), 64, 168, paint.GetWidth(), paint.GetHeight());
  epd.DisplayFrame_Partial();
  delay(300);
  epd.Sleep();
  delay(100);
  strcpy(timeBuf_old, timeBuf);  // å¤åˆ¶å†…å®¹

}

void handleRtcAlarmEvent() {

  rtc.clearAlarm(1);  // æ¸…é™¤ DS3231 çš„é—¹é’Ÿä¸­æ–­æ ‡å¿—
  DateTime now = rtc.now() + TimeSpan(0, 0, 1, 0);

  // æ£€æŸ¥æ˜¯å¦è·¨å¤©
  if (now.day() != lastDay) {
    lastDay = now.day();                  // æ›´æ–°è®°å½•
    currentState = STATE_EXAM_COUNTDOWN;  // é‡ç½®çŠ¶æ€ä¸ºè€ƒè¯•å€’è®¡æ—¶
    todayMin = 0;                         // é‡ç½®ä»Šå¤©çš„åˆ†é’Ÿæ•°
    drawicon(0);
    epd.DisplayFrame(); 
    displayTime(now);                     // æ˜¾ç¤ºå½“å‰æ—¶é—´
    setupNextAlarm();  // è®¾ç½®ä¸‹ä¸€åˆ†é’Ÿçš„é—¹é’Ÿ
    return;            // ç›´æ¥è¿”å›ï¼Œä¸å†ç»§ç»­æ‰§è¡Œ
  }


  if (now.minute() == TASK_MINUTE &&                                      // åˆ†é’Ÿ = 5
      (uint8_t)((now.hour() + 24 - TASK_BASE_H) % TASK_INTERVAL) == 0 &&  // (æ—¶-2) % 6 == 0
      !(now.day() == lastTaskDay && now.hour() == lastTaskHour)           // é¿å…åŒä¸€å°æ—¶é‡å¤
  ) {
    lastTaskDay = now.day();
    lastTaskHour = now.hour();
    checkMessages();
  }


  switch (currentState) {
    case STATE_EXAM_COUNTDOWN:
    drawicon(0);
    displayTime(now);
    break;

    case STATE_MEET_COUNTDOWN:
    drawicon(1);
    displayTime(now);
    break;
    
    case STATE_FOCUS_PAUSED:
      displayFocus(now);
      break;
    case STATE_FOCUS_RUNNING:
      displayFocus(now);
      break;
    case STATE_MQTT_MESSAGE:

      break;
    case STATE_LOW_BATTERY:

      break;
  }

  setupNextAlarm();  // è®¾ç½®ä¸‹ä¸€åˆ†é’Ÿçš„é—¹é’Ÿ
}

void enterDeepSleep() {
  // è®¾ç½®ä¸ºæ‰ç”µæ¨¡å¼
  ADCSRA &= ~_BV(ADEN);
  set_sleep_mode(SLEEP_MODE_PWR_DOWN);
  sleep_enable();
  // å…³é—­ BODï¼ˆBrown-Out Detectorï¼‰ï¼Œé™ä½ç¡çœ åŠŸè€—
  cli();  // è¿›å…¥åŸå­æ“ä½œåŒº
  sleep_bod_disable();
  sei();  // å¼€å¯ä¸­æ–­ï¼ˆå¿…é¡»åœ¨ sleep_cpu() å‰ï¼‰

  sleep_cpu();      // ğŸ’¥ å®é™…è¿›å…¥æ‰ç”µç¡çœ 
  sleep_disable();  // ğŸ›Œ é†’æ¥åæ¸…é™¤ç¡çœ å…è®¸æ ‡å¿—
}

void drawLowBatteryUI() {
  epd.SetFrameMemory_Base(IMAGE_DATA);
  epd.SetFrameMemory_WhiteBase(0, 128, 128, 168);
  epd.DisplayFrame();

  delay(5000);
  //debugSerial.println("sleep...");
  epd.Sleep();
  delay(100);
}
void drawFocus(int status) {
  
  if ((lastState != STATE_FOCUS_RUNNING) && (lastState != STATE_FOCUS_PAUSED)) {
    firstFlag = true;
    epd.Init();
    epd.SetFrameMemory_WhiteBase(0, 0, 128, 296);
    paint.SetWidth(14);
    paint.SetHeight(148);
    paint.SetRotate(ROTATE_90);
    paint.Clear(UNCOLORED);
    paint.DrawStringAt(0, 0, "Focus Mode", &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 110, 20, paint.GetWidth(), paint.GetHeight());
    paint.Clear(UNCOLORED);
    paint.DrawStringAt(0, 0, "Today:", &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 50, 60, paint.GetWidth(), paint.GetHeight());
    paint.Clear(UNCOLORED);
    paint.DrawStringAt(0, 0, "Total:", &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 10, 60, paint.GetWidth(), paint.GetHeight());
    epd.DisplayFrame();  // åˆ·æ–°
  }

  DateTime now = rtc.now();
  displayFocus(now);
}

void drawicon(int status) {
  firstFlag = true;
  epd.Init();
  // epd.ClearFrameMemory(0xFF);
  DateTime now = rtc.now();
  DateTime target(2025, 12, 20);                       // ç›®æ ‡æ—¶é—´
  DateTime now00(now.year(), now.month(), now.day());  // æ—¶åˆ†ç§’é»˜è®¤ 0
  TimeSpan remaining = target - now00;
  int days_left = remaining.days();  // å‰©ä½™å¤©æ•°
  if (days_left < 0)
    days_left = 0;  // å·²ç»è¿‡äº†ï¼Œè®¾ä¸º0å¤©
  int hundreds = days_left / 100;
  int tens = (days_left / 10) % 10;
  int units = days_left % 10;

  epd.SetFrameMemory_Base(IMAGE_DATA_ICON);  // å±…ä¸­ç»˜å›¾æ ‡
  epd.SetFrameMemory_WhiteBase(0, 128, 128, 168);
  paint.SetWidth(14);
  paint.SetHeight(148);
  paint.SetRotate(ROTATE_90);
  /* For simplicity, the arguments are explicit numerical coordinates */
  char dateBuf[11];  // "2025-07-23"
  snprintf(dateBuf, sizeof(dateBuf), "%04d-%02d-%02d", now.year(), now.month(), now.day());
  paint.Clear(UNCOLORED);
  paint.DrawStringAt(0, 1, dateBuf, &Font20, COLORED);
  epd.SetFrameMemory_Base(paint.GetImage(), 110, 140, paint.GetWidth(), paint.GetHeight());

  paint.SetWidth(64);
  paint.SetHeight(33);
  paint.SetRotate(ROTATE_90);
  paint.Clear(UNCOLORED);

  if (status == 0) {
    paint.DrawCharFromZeroAt(0, 0, units, &Font36, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 1, 196, paint.GetWidth(), paint.GetHeight());

    paint.Clear(UNCOLORED);
    paint.DrawCharFromZeroAt(0, 0, tens, &Font36, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 1, 163, paint.GetWidth(), paint.GetHeight());

    paint.Clear(UNCOLORED);
    paint.DrawCharFromZeroAt(0, 0, hundreds, &Font36, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 1, 130, paint.GetWidth(), paint.GetHeight());

    paint.SetWidth(16);
    paint.SetHeight(80);
    paint.SetRotate(ROTATE_90);
    paint.Clear(UNCOLORED);

    // åˆ¤æ–­æ˜¾ç¤º DAY æˆ– DAYS
    if (days_left == 1) {
      paint.DrawStringAt(0, 0, "DAY", &Font20, COLORED);
    } else {
      paint.DrawStringAt(0, 0, "DAYS", &Font20, COLORED);
    }
    epd.SetFrameMemory_Base(paint.GetImage(), 45, 230, paint.GetWidth(), paint.GetHeight());

    paint.Clear(UNCOLORED);

    paint.DrawStringAt(0, 0, "TO", &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 30, 230, paint.GetWidth(), paint.GetHeight());

    paint.Clear(UNCOLORED);
    paint.DrawStringAt(0, 0, "EXAM", &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 10, 230, paint.GetWidth(), paint.GetHeight());
  } else {

    eepromLoadTarget(target);
    remaining = target - now00;
    days_left = remaining.days();  // å‰©ä½™å¤©æ•°
    if (days_left < 0)
      days_left = 0;  // å·²ç»è¿‡äº†ï¼Œè®¾ä¸º0å¤©
    hundreds = days_left / 100;
    tens = (days_left / 10) % 10;
    units = days_left % 10;

    paint.DrawCharFromZeroAt(0, 0, units, &Font36, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 1, 196, paint.GetWidth(), paint.GetHeight());

    paint.Clear(UNCOLORED);
    paint.DrawCharFromZeroAt(0, 0, tens, &Font36, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 1, 163, paint.GetWidth(), paint.GetHeight());

    paint.Clear(UNCOLORED);
    paint.DrawCharFromZeroAt(0, 0, hundreds, &Font36, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 1, 130, paint.GetWidth(), paint.GetHeight());

    paint.SetWidth(16);
    paint.SetHeight(80);
    paint.SetRotate(ROTATE_90);
    paint.Clear(UNCOLORED);

    paint.DrawStringAt(0, 0, "DAYS", &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 45, 230, paint.GetWidth(), paint.GetHeight());

    paint.Clear(UNCOLORED);
    paint.DrawStringAt(0, 0, "MEET", &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 30, 230, paint.GetWidth(), paint.GetHeight());

    paint.Clear(UNCOLORED);
    paint.DrawStringAt(0, 0, "ZCQ", &Font20, COLORED);
    epd.SetFrameMemory_Base(paint.GetImage(), 10, 230, paint.GetWidth(), paint.GetHeight());
  }
  /*
  if(lastState != STATE_EXAM_COUNTDOWN && lastState != STATE_MEET_COUNTDOWN) {
    epd.DisplayFrame();  // åˆ·æ–°å…¨éƒ¨
  }
  
  delay(200);

  now = rtc.now();
  displayTime(now);*/
}

void setupNextAlarm() {
  DateTime now = rtc.now();

  // é»˜è®¤è®¾å®šç§’ = 58
  uint8_t next_sec = 58;
  uint8_t next_min = now.minute();
  uint8_t next_hour = now.hour();
  uint8_t next_day = now.day();
  uint8_t next_month = now.month();
  uint16_t next_year = now.year();

  // å¦‚æœå½“å‰å·²ç»è¶…è¿‡58ç§’ï¼Œåˆ™è®¾ç½®ä¸ºä¸‹ä¸€åˆ†é’Ÿ
  if (now.second() >= 58) {
    next_min += 1;
    if (next_min >= 60) {
      next_min = 0;
      next_hour += 1;
      if (next_hour >= 24) {
        next_hour = 0;
        // å¤„ç†è·¨å¤©
        DateTime tomorrow = now + TimeSpan(1, 0, 0, 0);
        next_day = tomorrow.day();
        next_month = tomorrow.month();
        next_year = tomorrow.year();
      }
    }
  }

  // æ¸…é™¤ä¹‹å‰çš„é—¹é’Ÿè®¾ç½®
  rtc.clearAlarm(1);

  // è®¾ç½® Alarm1 è§¦å‘æ—¶é—´
  DateTime alarmTime(next_year, next_month, next_day, next_hour, next_min, next_sec);
  rtc.setAlarm1(alarmTime, DS3231_A1_Second);
}

void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200);
  //debugSerial.begin(115200);

  pinMode(PMOS_CTRL_PIN, OUTPUT);

  digitalWrite(PMOS_CTRL_PIN, HIGH);  // åˆå§‹çŠ¶æ€å…³ç”µæºï¼ˆæˆªæ­¢ï¼‰


  // é…ç½® D2ã€D3 ä¸ºä¸Šæ‹‰è¾“å…¥
  pinMode(2, INPUT_PULLUP);              // INT0
  pinMode(3, INPUT_PULLUP);              // INT1
  EIMSK |= (1 << INT0) | (1 << INT1);    // å¯ç”¨å¤–éƒ¨ä¸­æ–­ INT0 / INT1
  EICRA |= (1 << ISC01) | (1 << ISC11);  // ä¸‹é™æ²¿è§¦å‘ INT0 / INT1
  pinMode(A1, INPUT_PULLUP);             // PCINT23
  PCICR |= (1 << PCIE1);                 // å¯ç”¨ Port Cï¼ˆPCINT[14:8]ï¼‰çš„ä¸­æ–­åŠŸèƒ½
  PCMSK1 |= (1 << PCINT9);               // å…è®¸ A1ï¼ˆPC1ï¼‰ç”µå¹³å˜åŒ–è§¦å‘ä¸­æ–­

  Wire.begin();
  //debugSerial.println("Serial Begin");
  delay(10);
  epd.Init();
  epd.ClearFrameMemory(0xFF);  // bit set = white, bit reset = black
  epd.DisplayFrame();
  delay(100);

  batteryMonitorBegin();  // åˆå§‹åŒ–
  uint16_t batteryVoltage = readBatteryVoltage_mv();
  if (batteryVoltage < 820) {
    //debugSerial.println("Low Battery, Entering Low Battery Mode");
    currentState = STATE_LOW_BATTERY;
    drawLowBatteryUI();
    enterDeepSleep();  // è¿›å…¥æ·±åº¦ç¡çœ 
  } else {
    //debugSerial.print("Battery Voltage: ");
    //debugSerial.print(batteryVoltage);
    //debugSerial.println(" mV");
  }
  if (!rtc.begin()) {
    while (1)
      delay(10);
  }
  pinMode(13, INPUT_PULLUP);  // D13è¿æ¥DS3231 INTï¼Œå¼€æ¼ï¼Œå¿…é¡»ä¸Šæ‹‰
  // ç¦ç”¨DS3231æ–¹æ³¢ï¼Œå¯ç”¨ä¸­æ–­æ¨¡å¼
  rtc.writeSqwPinMode(DS3231_OFF);
  // å¯ç”¨ PCINT0 ä¸­æ–­ï¼ŒPCINT5 = D13
  PCICR |= (1 << PCIE0);    // ä½¿èƒ½ Port Bï¼ˆPB0â€“PB7ï¼‰çš„ PCINT ä¸­æ–­
  PCMSK0 |= (1 << PCINT5);  // å¯ç”¨ D13 çš„ PCINT

  drawicon(0);
  epd.DisplayFrame(); 
  displayTime(rtc.now());  // æ˜¾ç¤ºå½“å‰æ—¶é—´
  setupNextAlarm();

  lastDisplayTime = rtc.now();
  eepromSaveTarget(DateTime(2025, 07, 30));
  loadTotalMin(totalMin);
}

void loop() {

  wakeUp = false;
  alarmTriggered = false;
  enterDeepSleep();

  if (button1Pressed) {
    button1Pressed = false;
    switchState(EVENT_BUTTON1);
  }

  if (button2Pressed) {
    button2Pressed = false;
    switchState(EVENT_BUTTON2);
  }

  if (button3Pressed) {
    button3Pressed = false;
    switchState(EVENT_BUTTON3);
  }

  if (alarmTriggered) {
    alarmTriggered = false;
    handleRtcAlarmEvent();
  }
}
